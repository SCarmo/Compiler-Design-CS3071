COMPILER Tastier

   const int // object kinds
      var = 0, proc = 1, constant = 2, scope = 3,  str = 4;

   const int // types
      undef = 0, integer = 1, boolean = 2;

   public SymbolTable tab;
   public CodeGenerator gen;

/*-------------------------------------------------------------------------------------------*/

CHARACTERS

  cr  = '\r'.
  lf  = '\n'.
  tab = '\t'.
  digit = "0123456789".
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
  stringCh = ANY - '"' - '\\' - cr - lf - tab.
  printableCh = '\u0020' .. '\u007e'.

/*-------------------------------------------------------------------------------------------*/

TOKENS

  number = digit { digit }.
  ident = letter { letter | digit }.
  string = '"' {stringCh | '\\' printableCh} '"'.

/*-------------------------------------------------------------------------------------------*/

COMMENTS FROM "//" TO lf
COMMENTS FROM "/*" TO "*/" NESTED

/*-------------------------------------------------------------------------------------------*/

IGNORE cr + lf + tab

/*-------------------------------------------------------------------------------------------*/

PRODUCTIONS

AddOp<out Op op>
=                        (. op = Op.ADD; .)
  ( '+'
  | '-'                  (. op = Op.SUB; .)
  )
.

/*-------------------------------------------------------------------------------------------*/

Expr<out int reg,        // load value of Expr into register
     out int type>       (. int typeR, regR; Op op; .)
  = SimExpr<out reg,
            out type>
  [ RelOp<out op>
    SimExpr<out regR,
            out typeR>   (.
                            if (type == typeR) {
                               type = boolean;
                               gen.RelOp(op, reg, regR);
                            }
                            else SemErr("incompatible types");
                         .)

    [
      TernaryOp<out reg, out type>
    ]
   ]                     (. gen.ClearRegisters(); .)
.

/*-------------------------------------------------------------------------------------------*/

Primary<out int reg,     // load Primary into register
        out int type>    (. int n; Obj obj; string name; .)
=                        (.
                            type = undef;
                            reg = gen.GetRegister();
                         .)

  ( Ident<out name>      (.
                            obj = tab.Find(name); type = obj.type;
                         .)

  [ "."

  Ident<out name>        (.
                            obj = tab.Find(obj.name+name);
                         .)
  ]
                         (. if (obj.kind == var || obj.kind == constant) {
                               if (obj.level == 0)
                                  gen.LoadGlobal(reg, obj.adr, name);
                               else
                                  gen.LoadLocal(reg, tab.curLevel-obj.level, obj.adr, name);
                               if (type == boolean)
                               // reset Z flag in CPSR
                                  gen.ResetZ(reg);
                            }
                            else SemErr("variable/constant expected");
                         .)


  | number               (.
                            type = integer;
                            n = Convert.ToInt32(t.val);
                            gen.LoadConstant(reg, n);
                         .)

  | '-'
    Primary<out reg,
            out type>     (.
                            if (type == integer)
                               gen.NegateValue(reg);
                            else SemErr("integer type expected");
                         .)

  | "true"               (.
                               type = boolean;
                               gen.LoadTrue(reg);
                         .)

  | "false"              (.
                               type = boolean;
                               gen.LoadFalse(reg);
                         .)

  | '('
    Expr<out reg,
         out type>
    ')'

  )
.

/*-------------------------------------------------------------------------------------------*/

Ident<out string name>
= ident                  (. name = t.val; .)
.

/*-------------------------------------------------------------------------------------------*/

String<out string text>
= string                  (. text = t.val; .)
.

/*-------------------------------------------------------------------------------------------*/

MulOp<out Op op>
=                        (. op = Op.MUL; .)
  ( '*'
  | ("div" | "DIV")      (. op = Op.DIV; .)
  | ("mod" | "MOD")      (. op = Op.MOD; .)
  )
.

/*-------------------------------------------------------------------------------------------*/

ProcDecl<string progName>
                         (. Obj obj; string procName; .)
= "void"
  Ident<out procName>    (.
                            obj = tab.NewObj(procName, proc, undef);
                            if (procName == "main")
                               if (tab.curLevel == 0)
                                  tab.mainPresent = true;
                               else SemErr("main not at lexic level 0");
                            tab.OpenScope();
                         .)
  '(' ')'
  '{'
     {
     VarDecl
     }
     {
     StructInit
     }
     {
     ProcDecl<progName>
     }                   (.
                            if (procName == "main")
                               gen.Label("Main", "Body");
                            else {
                               gen.ProcNameComment(procName);
                               gen.Label(procName, "Body");
                            }
                         .)
     Stat { Stat }
  '}'                    (.
                            if (procName == "main") {
                               gen.StopProgram(progName);
                               gen.Enter("Main", tab.curLevel, tab.topScope.nextAdr);
                            } else {
                               gen.Return(procName);
                               gen.Enter(procName, tab.curLevel, tab.topScope.nextAdr);
                            }
                            tab.CloseScope();
                         .)
.

/*-------------------------------------------------------------------------------------------*/

RelOp<out Op op>
=                        (. op = Op.EQU; .)
  ( "="
  | '<'                  (. op = Op.LSS; .)
  | '>'                  (. op = Op.GTR; .)
  | "!="                 (. op = Op.NEQ; .)
  | "<="                 (. op = Op.LEQ; .)
  | ">="                 (. op = Op.GEQ; .)
  )
.


/*-------------------------------------------------------------------------------------------*/
TernaryOp<out int reg,  //Loads value of ? x : y int register based on last RelOp
          out int type>   (. int l0, l1; .)
=
  '?'                      (.
                            gen.ClearRegisters();
                            l0 = gen.NewLabel();
                            l1 = gen.NewLabel();
                          .)
                          (. gen.BranchFalse(l0);.)
  Expr<out reg, out type> (. gen.Branch(l1); .)
  ':'                     (. gen.Label(l0); .)
  Expr<out reg, out type>
                          (. gen.Label(l1); .)
.
/*-------------------------------------------------------------------------------------------*/


SimExpr<out int reg,     //load value of SimExpr into register
        out int type>    (. int typeR, regR; Op op; .)
= Term<out reg,
       out type>
  { AddOp<out op>
    Term<out regR,
         out typeR>      (.
                            if (type == integer && typeR == integer)
                               gen.AddOp(op, reg, regR);
                            else SemErr("integer type expected");
                         .)
  }
.

/*-------------------------------------------------------------------------------------------*/

Stat                     (. int type; string name; Obj obj; int reg; .)
= Ident<out name>        (. obj = tab.Find(name); .)
  [ "."

    Ident<out name>     (.
                            obj = tab.Find(obj.name+name);
                        .)
  ]
  ( ":="                 (.
                			      if (obj.kind == constant)
			                         SemErr("cannot re-assign constant");

                            if (obj.kind != var)
                               SemErr("cannot assign to procedure");
                         .)
    Expr<out reg,
         out type> ';'   (.
                            if (type == obj.type)
                               if (obj.level == 0)
                                  gen.StoreGlobal(reg, obj.adr, name);
			                         else gen.StoreLocal(reg, tab.curLevel-obj.level, obj.adr, name);
			                      else SemErr("incompatible types"); // missing line
                         .)
  | '(' ')' ';'          (.
                            if (obj.kind == proc)
                               gen.Call(name);
                            else SemErr("object is not a procedure");
                         .)



// while implementing for loops i noticed that I had to do k= k+1 rather than k++
// so I decided to impement post increment and decrement
  | "++"                 (. int NewReg; .)

                         (.
                            if(obj.type != integer)
                              SemErr("type integer expected for post incrementation");
                            reg = gen.GetRegister();
                            NewReg = gen.GetRegister();
                            // store 1 in reg
                            gen.LoadConstant(NewReg, 1);

                            //load value of identifier into next reg
                            if(obj.level == 0)
                              gen.LoadGlobal(reg,obj.adr, obj.name);
                            else
                              gen.LoadLocal(reg, tab.curLevel-obj.level, obj.adr, name);
                            // perform addition
                            gen.AddOp(Op.ADD, reg, NewReg);

                            // store new value back to same address
                            if (obj.level == 0)
                              gen.StoreGlobal(reg, obj.adr, name);
			                      else
                              gen.StoreLocal(reg, tab.curLevel-obj.level, obj.adr, name);
                         .)
    ";"

    | "--"                 (. int NewReg; .)

                           (.
                              if(obj.type != integer)
                                SemErr("type integer expected for post decrementaion");
                              reg = gen.GetRegister();
                              NewReg = gen.GetRegister();
                              // store 1 in reg
                              gen.LoadConstant(NewReg, 1);

                              //load value of identifier into next reg
                              if(obj.level == 0)
                                gen.LoadGlobal(reg,obj.adr, obj.name);
                              else
                                gen.LoadLocal(reg, tab.curLevel-obj.level, obj.adr, name);
                              // perform subtraction
                              gen.AddOp(Op.SUB, reg, NewReg);

                              // store new value back to same address
                              if (obj.level == 0)
                                gen.StoreGlobal(reg, obj.adr, name);
                              else
                                gen.StoreLocal(reg, tab.curLevel-obj.level, obj.adr, name);
                           .)
      ";"

  )

| "if"                   (. int l1, l2; l1 = 0; .)
  Expr<out reg,
       out type>         (.
                            if (type == boolean) {
                               l1 = gen.NewLabel();
                               gen.BranchFalse(l1);
                            }
                            else SemErr("boolean type expected");
                         .)
      Stat               (.
                            l2 = gen.NewLabel();
                            gen.Branch(l2);
                            gen.Label(l1);
                         .)
      [ "else"
      Stat
      ]               	 (. gen.Label(l2); .)

| "while"                (.
                            int l1, l2;
                            l1 = gen.NewLabel();
                            gen.Label(l1); l2=0;
                         .)
  Expr<out reg,
       out type>         (.
                            if (type == boolean) {
                               l2 = gen.NewLabel();
                               gen.BranchFalse(l2);
                            }
                            else SemErr("boolean type expected");
                         .)
   Stat                  (.
                            gen.Branch(l1);
                            gen.Label(l2);
                         .)

// implementing for loop
| "for" "("

  Stat                   (.
                            int l1, l2;
                            l1 = gen.NewLabel();
                            gen.Label(l1);  // label to loop
                            l2 = 0;
                         .)
  Expr<out reg,
       out type> ";"     (.
                            if (type == boolean){
                              l2 = gen.NewLabel();
                              gen.BranchFalse(l2);
                            }
                            else SemErr("boolean type expected"); // must be comparison in for loop
                         .)
  Stat ")"               // Stat will execute before first loop... not sure how to solve this???

                         // Also stat alows for switch statement and while loops and even another for loop to be placed
                         // but this would be down to bad programming
  "{"
      {Stat}  // 0...n stats
  "}"                    (.
                            gen.Branch(l1);
                            gen.Label(l2);  // label to exit
                         .)


| "read"
  Ident<out name> ';'    (.
                            obj = tab.Find(name);
                            if (obj.type == integer) {
                               gen.ReadInteger();
                               if (obj.level == 0)
                                  gen.StoreGlobal(0, obj.adr, name);
                               else gen.StoreLocal(0, tab.curLevel-obj.level, obj.adr, name);
                            }
                            else SemErr("integer type expected");
                         .)

| "write"                (. string text; .)
  (
    Expr<out reg,
         out type>       (.
                            switch (type) {
                               case integer: gen.WriteInteger(reg, false);
                                             break;
                               case boolean: gen.WriteBoolean(false);
                                             break;
                            }
                         .)
  | String<out text>     (. gen.WriteString(text); .)
  )
  ';'

| "writeln"
  // note - for strings use write with \n in text
  Expr<out reg,
       out type>           (.
                              switch (type) {
                                 case integer: gen.WriteInteger(reg, true);
                                               break;
                                 case boolean: gen.WriteBoolean(true);
                                               break;
                              }
                           .)
  ';'

| '{'                      (. tab.OpenSubScope(); .)
  { VarDecl }
  Stat { Stat }
  '}'                      (. tab.CloseSubScope(); .)

| "switch" "("             (.
                              int breakLabel, switchReg, caseReg; breakLabel = 0;
                              breakLabel = gen.NewLabel();
                           .)
  Expr<out switchReg,
       out type>  ")"
  "{"
    {                       (. gen.GetRegister(); .)
      "case"
      Expr<out caseReg,
      out type> ":"         (. int l1 = 0;
                               l1 = gen.NewLabel();
                               gen.RelOp(Op.EQU, caseReg, switchReg);
                               gen.BranchFalse(l1);
                            .)

      {Stat}
      [ "break" ";"          (. gen.Branch(breakLabel); .)
      ]                      (. gen.Label(l1); .)
    }
    [ "default" ":" {Stat}  ]
  "}"                       (. gen.Label(breakLabel); .)
.

/*-------------------------------------------------------------------------------------------*/

Tastier                  (. string progName; .)
  = "program"
  Ident<out progName>    (. tab.OpenScope(); .)
  '{'
    { ConstDef }
    { VarDecl }          // global variables
    { StructDecl}
    { ProcDecl<progName> }
                         (. tab.CloseScope(); .)
  '}'
.

/*-------------------------------------------------------------------------------------------*/

Term<out int reg,        // load value of Term into register
     out int type>       (. int typeR, regR; Op op; .)
  = Primary<out reg,
            out type>
  { MulOp<out op>
    Primary<out regR,
            out typeR>
                         (.
                            if (type == integer && typeR == integer)
                               gen.MulOp(op, reg, regR);
                            else SemErr("integer type expected");
                         .)
  }
.

/*-------------------------------------------------------------------------------------------*/

Type<out int type>
=                        (. type = undef; .)
 ( "int"                 (. type = integer; .)
 | "bool"                (. type = boolean; .)
 )
.

/*-------------------------------------------------------------------------------------------*/

VarDecl                  (. string name; int type; .)
= Type<out type>
  Ident<out name>        (. tab.NewObj(name, var, type); .)
  { ","
    Ident<out name>      (. tab.NewObj(name, var, type); .)
  } ";"
.

/*-------------------------------------------------------------------------------------------*/

ConstDef                  (. string name; int reg, type; Obj obj; .)
= "const"
  Type<out type>
  Ident<out name>        (. obj = tab.NewObj(name, constant, type); .)
 "="
  Primary <out reg,
	   out type>	         (.
                            if (type == obj.type)
                               if (obj.level == 0)
                                  gen.StoreGlobal(reg, obj.adr, name);
                               else SemErr("Cannot define constant locally");
			                      else SemErr("incompatible types");
                         .)
 ";"
.

/*-------------------------------------------------------------------------------------------*/
/*
                    "struct" ident "{"
                      {variable declarations}
                    "}"
*/
StructDecl                (. string name; int reg; Obj obj;.)
= "struct"

  Ident<out name>         (.
                             reg = gen.GetRegister();
                             obj = tab.NewObj(name, str, undef);
                          .)

  "{"

  {StructVarDecl}

  "};"                    (.
                             obj.isStruct = true;
                             // must be stored globally
                             if(obj.level ==0)
                                gen.StoreGlobal(reg, obj.adr, obj.name);
                             else
                                SemErr("Cannot store struct locally");
                          .)
.

/*-------------------------------------------------------------------------------------------*/
/*
                struct Person me;
*/
StructInit              (. string name; Obj obj, NewObject; .)
= "struct"

Ident<out name>         (.
                            obj = tab.Find(name);

                            if(!obj.isStruct)
                              SemErr("type struct expected");
                        .)

Ident<out name>        (.
                           NewObject = tab.NewObj(name, str, undef);
                           NewObject.isStruct = true;
                           obj = obj.next;
                           // loop through variables in struct and create copys associated with new struct name
                           while(obj.isStruct){
                             NewObject = tab.NewObj(name+obj.name, var, obj.type);
                             //   for: struct Person me;  ||
                             //                           \/
                             // the new obj name is      meage     as in me.age
                             NewObject.isStruct = true;
                             obj = obj.next;
                           }
                        .)

";"
.


/*-------------------------------------------------------------------------------------------*/

StructVarDecl           (. string name; int type; Obj obj; .)
=
Type<out type>
  Ident<out name>        (.
                            obj = tab.NewObj(name, var, type);
                            // same as previous VarDecl except SymTab.cs was edited
                            // to contain the boolean "isStruct" which determines if the Ident
                            // is involved with a struct
                            obj.isStruct = true;
                         .)
  { ","
    Ident<out name>      (.
                            obj = tab.NewObj(name, var, type);
                            obj.isStruct = true;
                         .)
  } ";"
.

END Tastier.
